#!/usr/bin/env perl

package jameson;

use 5.016;
use warnings;
use strict;

use FindBin;
use lib "$FindBin::Bin/lib";

use AnyEvent;
use AnyEvent::Log;
use AnyEvent::IRC::Client;

use Module::Pluggable
    instantiate => "new",
    search_path => "jameson::plugin";

my @plugins = __PACKAGE__->plugins;

for my $plugin (@plugins) {
    $plugin->init if $plugin->enabled;
}

if (@ARGV < 4) {
    say "usage: jameson <host> <port> <nick> <channels> [<nickserv pass>]";
    exit 1;
}

my ($HOST, $PORT, $NICK, $CHANNELS, $NSPASS) = @ARGV;
my @CHANNELS = split ',', $CHANNELS;

AnyEvent::Log::ctx->log_cb(sub { print STDOUT shift; 0 });
my $log = AnyEvent::Log::logger("info");

my $c;
my $condvar = sub { $c };

my $timer;
my $con = new AnyEvent::IRC::Client;

my %joined;
my $tries = 3;

$con->reg_cb(
    registered => sub {
        $log->("registered, resetting connection counter");
        $tries = 4;
        if ($NSPASS) {
            $log->("identifying ourselves to NickServ");
            $con->send_srv(PRIVMSG => "NickServ", "IDENTIFY $NSPASS");
        }
        for my $channel (@CHANNELS) {
            $log->("joining $channel");
            $con->send_srv(JOIN => $channel);
        }
    },
    connect => sub {
        my (undef, $err) = @_;
        return unless $err;
        $log->("connect failed: $err");
        $condvar->()->send;
    },
    disconnect => sub {
        $log->("disconnected");
        $condvar->()->send;
    },
    join => sub {
        return if not pop @_;
        my $channel = pop @_;
        $log->("joined $channel");
        $joined{$channel} = 1;

        for my $plugin (@plugins) {
            $plugin->joined($con, $channel) if $plugin->enabled;
        }
    },
    ctcp_action => sub {
        my (undef, $from, $channel, $msg, $type) = @_;
        return unless $type eq "PRIVMSG";

        return unless $channel =~ m/^#/;
        if (not $joined{$channel}) {
            $log->("got message on $channel, but not joined, ignoring: $msg");
            return;
        }

        for my $plugin (@plugins) {
            $plugin->publicmsg($con, $channel, $from, $msg, 0) if $plugin->enabled;
        }
    },
    publicmsg => sub {
        my $ircmsg = pop @_;
        my ($channel, $msg) = @{$ircmsg->{params}};

        if (not $joined{$channel}) {
            $log->("got message on $channel, but not joined, ignoring: $msg");
            return;
        }

        my (undef, $from) = $con->split_nick_mode($ircmsg->{prefix});
        my ($text) = $msg =~ m/^$NICK\b.?\s*(.*)/i;

        # ignore the github bot
        # XXX this is a dumb way to do it
        return if $from eq "Pioneer";

        my $direct = 1;
        if (not $text) {
            $text = $msg;
            $direct = 0;
        }

        for my $plugin (@plugins) {
            $plugin->publicmsg($con, $channel, $from, $text, $direct) if $plugin->enabled;
        }
    },
);
$con->enable_ping(5, sub {
    $log->("ping failed, attempting reconnect");
    $c->send;
});

while ($tries > 0) {
    $c = AnyEvent::CondVar->new;
    $log->("connecting to $HOST:$PORT as $NICK ($tries attempts remain)");
    $con->connect($HOST, $PORT, { nick => $NICK });
    $log->("entering main loop");
    $c->recv;
    $log->("got shutdown signal");
    $con->disconnect if $con->is_connected;
    $tries--;
}

$log->("no tries remaining, exiting");

exit 0;


package jameson::util;

use AnyEvent::HTTP;

sub shorten {
    state %cache;

    my ($url, $cb) = @_;

    if ($cache{$url}) {
        $log->("shortened $url to $cache{$url} (cached)");
        $cb->($cache{$url});
    }
    else {
        http_get("http://is.gd/create.php?url=$url&format=simple", sub {
            my ($body, $hdr) = @_;
            if ($hdr->{Status} =~ m/^2/) {
                $cache{$url} = $body;
                $log->("shortened $url to $body");
                $cb->($body);
            }
            else {
                $log->("failed to shorten $url: $hdr->{Status}");
                $cb->($url);
            }
        });
    }
}


package jameson::plugin;

sub new {
    my ($class) = @_;

    return bless \do {my $dummy}, $class;
}

sub log {
    my ($self, $msg) = @_;
    my ($plugin) = ref($self) =~ m/::([^:]+)$/;
    $log->("$plugin: $msg");
}

sub enabled { 1 }

sub init {}
sub publicmsg {}
sub joined {}
